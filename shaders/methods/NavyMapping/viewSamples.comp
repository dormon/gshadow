#version 440 core

#ifndef WORKGROUP_SIZE_X
#define WORKGROUP_SIZE_X 8
#endif//WORKGROUP_SIZE_X

#ifndef WORKGROUP_SIZE_Y
#define WORKGROUP_SIZE_Y 8
#endif//WORKGROUP_SIZE_Y

layout(local_size_x=WORKGROUP_SIZE_X,local_size_y=WORKGROUP_SIZE_Y)in;

layout(      binding=0)uniform sampler2D position;
layout(rg32f,binding=1)uniform   image2D viewSamples;

uniform uvec2 windowSize=uvec2(1024u,1024u);
uniform mat4  mvp;

#define COORD gl_GlobalInvocationID.xy

#ifdef USE_PARABOLOID
uniform float near;
uniform float far ;
#endif


void main(){
  if(any(greaterThanEqual(COORD,windowSize)))return;
  vec2 final=vec2(2.);
  vec4 pos=texelFetch(position,ivec2(COORD),0);
  if(!any(isnan(pos))){
#ifdef USE_PARABOLOID
    vec4 shadowCoord=dualParaboloidProjection(mvp,pos.xyz,near,far);
#else//USE_PARABOLOID
    vec4 shadowCoord=mvp*vec4(pos.xyz,1);
#endif//USE_PARABOLOID

    if( all(greaterThan(shadowCoord.xyz,-shadowCoord.www))&&
        all(lessThan   (shadowCoord.xyz,+shadowCoord.www))){
      final=shadowCoord.xy/shadowCoord.w*.5+.5;
    }
  }
  imageStore(viewSamples,ivec2(COORD),final.xyxy);
}
